plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        expand(project.properties)
    }
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
}

dependencies {
    compileOnly('net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT') {
        exclude group: 'com.mojang'
    }

    implementation 'redis.clients:jedis:3.6.3'
    implementation 'org.apache.commons:commons-pool2:2.10.0'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = 'UTF-8'
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'redis.clients.jedis', project.group + '.internal.jedis'
    relocate 'redis.clients.util', project.group + '.internal.jedisutil'
    relocate 'org.apache.commons.pool', project.group + '.internal.commonspool'
    relocate 'com.squareup.okhttp', project.group + '.internal.okhttp'
    relocate 'okio', project.group + '.internal.okio'
}